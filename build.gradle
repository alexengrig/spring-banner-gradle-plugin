plugins {
    id 'com.gradle.plugin-publish' version '1.0.0'
    id 'com.gradle.enterprise' version '3.11.2'
}

group = 'dev.alexengrig.gradle'
version = "${springBannerPluginVersion}"
description = 'Spring Banner Gradle Plugin for generating banners.'

gradlePlugin {
    plugins {
        springBannerPlugin {
            id = 'dev.alexengrig.spring-banner'
            displayName = 'Spring Banner Gradle Plugin'
            description = project.description
            implementationClass = 'dev.alexengrig.gradle.spring.banner.SpringBannerPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/alexengrig/spring-banner-gradle-plugin'
    vcsUrl = 'https://github.com/alexengrig/spring-banner-gradle-plugin'
    tags = ['spring']
}

gradleEnterprise {
    if (System.getenv("CI") != null) {
        buildScan {
            publishAlways()
            termsOfServiceUrl = "https://gradle.com/terms-of-service"
            termsOfServiceAgree = "yes"
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    withSourcesJar()
    withJavadocJar()
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

javadoc {
    options.encoding = 'UTF-8'
    options.addBooleanOption('Xdoclint:all,-missing', true)
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'Alexengrig Dev.'
        )
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.github.dtmo.jfiglet:jfiglet:${jfigletVersion}"
}

test {
    useJUnitPlatform()
}
