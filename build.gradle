plugins {
    id 'com.gradle.plugin-publish'
    id 'org.unbroken-dome.test-sets'
    id 'jacoco'
}

group = 'io.github.alexengrig.gradle'
version = "${projectVersion}"
description = 'Spring Banner Gradle Plugin for generating banners.'

gradlePlugin {
    plugins {
        springBannerPlugin {
            id = 'io.github.alexengrig.spring-banner'
            displayName = 'Spring Banner Gradle Plugin'
            description = "Generating Spring Boot 'banner.txt'."
            implementationClass = 'io.github.alexengrig.gradle.spring.banner.SpringBannerPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/alexengrig/spring-banner-gradle-plugin'
    vcsUrl = 'https://github.com/alexengrig/spring-banner-gradle-plugin'
    tags = ['spring', 'spring-framework', 'spring-boot', 'banner']
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

javadoc { options.encoding = 'UTF-8' }

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'Alexengrig Dev.'
        )
    }
}

repositories { mavenCentral() }

dependencies {
    implementation "com.github.dtmo.jfiglet:jfiglet:${jfigletVersion}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junit5Version}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junit5Version}"

    testImplementation "org.assertj:assertj-core:${assertJVersion}"

    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
}

testSets {
    functionalTest
    integrationTest
}

tasks.withType(Test) {
    useJUnitPlatform()
    check.dependsOn it
}

jacoco { toolVersion = "${jacocoToolVersion}" }

jacocoTestReport.dependsOn test
jacocoIntegrationTestReport.dependsOn integrationTest
jacocoFunctionalTestReport.dependsOn functionalTest

tasks.register('jacocoAllTestsReport', JacocoReport) {
    group = JavaBasePlugin.VERIFICATION_GROUP
    description = 'Generates code coverage report for the all tests.'
    sourceSets(project.sourceSets.main)
    final def testTasks = tasks.withType(Test).toArray(Task[]::new)
    executionData(testTasks)
    dependsOn testTasks
    jacocoTestCoverageVerification.dependsOn it
}

tasks.withType(JacocoReport) {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0
            }
        }
    }
    check.dependsOn it
}
